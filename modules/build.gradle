apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
	portalTestVersion = "5.1.2" 
    portalTestIntegrationVersion = "5.0.8" 
}

task removeDefaultTestBundles(type: Delete) { 
    delete fileTree("${gradle.liferayWorkspace.homeDir}/osgi/test") { 
        include '*.jar' 
    } 
} 
 
configure(subprojects.findAll { !it.subprojects }) {
	afterEvaluate { project ->
		compileJava {
			dependsOn "formatSource"
			options.encoding = "UTF-8"
		}

		dependencies {
			testCompile group: "junit", name: "junit", version: "4.12"
		}

		deploy {
			exclude "**/*source.formatter*.jar"
		}
		
		project.plugins.withId('com.liferay.test.integration') {
            configurations {
                testModules
            }

            dependencies {
				testIntegrationCompile group: "com.liferay.portal", name: "com.liferay.portal.test", version: portalTestVersion 
                testIntegrationCompile group: "com.liferay.portal", name: "com.liferay.portal.test.integration", version: portalTestIntegrationVersion 
 
                testModules group: "com.liferay.portal", name: "com.liferay.portal.test", version: portalTestVersion 
                testModules group: "com.liferay.portal", name: "com.liferay.portal.test.integration", version: portalTestIntegrationVersion 
 
                testModules group: "com.liferay", name: "com.liferay.petra.concurrent", version: "1.1.2" 
                testModules group: "com.liferay", name: "com.liferay.petra.memory", version: "1.0.1" 
                testModules group: "com.liferay", name: "com.liferay.petra.lang", version: "1.1.0"

				testModules group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.core", version: "1.1.7"
            }

            copyTestModules.dependsOn removeDefaultTestBundles 
 
            startTestableTomcat.dependsOn rootProject.deployTestableModules
            startTestableTomcat.checkTimeout 1200000 
   
            testIntegration {
                testClassesDir = sourceSets.testIntegration.output.classesDir
            }
        }
	}
}
